# File: terraform-azure-redis-cache-module/main.tf

/**
 * Module: terraform-azure-redis-cache-module
 * Description: Deploys a fully customizable Azure Redis Cache instance
 * Registry Reference: https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/redis_cache
 */

resource "azurerm_redis_cache" "this" {
  name                               = var.redis_cache_name
  location                           = var.location
  resource_group_name                = var.resource_group_name
  capacity                           = var.capacity
  family                             = var.family
  sku_name                           = var.sku_name
  access_keys_authentication_enabled = var.access_keys_authentication_enabled
  non_ssl_port_enabled               = var.non_ssl_port_enabled
  minimum_tls_version                = var.minimum_tls_version
  private_static_ip_address          = var.private_static_ip_address
  public_network_access_enabled      = var.public_network_access_enabled
  replicas_per_master                = var.replicas_per_master
  replicas_per_primary               = var.replicas_per_primary
  redis_version                      = var.redis_version
  tenant_settings                    = var.tenant_settings
  shard_count                        = var.shard_count
  subnet_id                          = var.subnet_id
  tags                               = var.tags
  zones                              = var.availability_zone ? var.zones : null

  dynamic "identity" {
    for_each = var.identity != null ? [var.identity] : []
    content {
      type         = identity.value.type
      identity_ids = lookup(identity.value, "identity_ids", null)
    }
  }

  dynamic "patch_schedule" {
    for_each = var.patch_schedule
    content {
      day_of_week        = patch_schedule.value.day_of_week
      start_hour_utc     = lookup(patch_schedule.value, "start_hour_utc", null)
      maintenance_window = lookup(patch_schedule.value, "maintenance_window", null)
    }
  }

  dynamic "redis_configuration" {
    for_each = var.redis_configuration != null ? [var.redis_configuration] : []
    content {
      aof_backup_enabled                      = lookup(redis_configuration.value, "aof_backup_enabled", null)
      aof_storage_connection_string_0         = lookup(redis_configuration.value, "aof_storage_connection_string_0", null)
      aof_storage_connection_string_1         = lookup(redis_configuration.value, "aof_storage_connection_string_1", null)
      authentication_enabled                  = lookup(redis_configuration.value, "authentication_enabled", null)
      active_directory_authentication_enabled = lookup(redis_configuration.value, "active_directory_authentication_enabled", null)
      maxmemory_reserved                      = lookup(redis_configuration.value, "maxmemory_reserved", null)
      maxmemory_delta                         = lookup(redis_configuration.value, "maxmemory_delta", null)
      maxmemory_policy                        = lookup(redis_configuration.value, "maxmemory_policy", null)
      data_persistence_authentication_method  = lookup(redis_configuration.value, "data_persistence_authentication_method", null)
      maxfragmentationmemory_reserved         = lookup(redis_configuration.value, "maxfragmentationmemory_reserved", null)
      rdb_backup_enabled                      = lookup(redis_configuration.value, "rdb_backup_enabled", null)
      rdb_backup_frequency                    = lookup(redis_configuration.value, "rdb_backup_frequency", null)
      rdb_backup_max_snapshot_count           = lookup(redis_configuration.value, "rdb_backup_max_snapshot_count", null)
      rdb_storage_connection_string           = lookup(redis_configuration.value, "rdb_storage_connection_string", null)
      notify_keyspace_events                  = lookup(redis_configuration.value, "notify_keyspace_events", null)
      storage_account_subscription_id         = lookup(redis_configuration.value, "storage_account_subscription_id", null )
    }
  }
}


